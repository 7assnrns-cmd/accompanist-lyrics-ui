import com.vanniktech.maven.publish.JavadocJar
import com.vanniktech.maven.publish.KotlinMultiplatform

plugins {
    alias(libs.plugins.jetbrains.kotlin.multiplatform)
    alias(libs.plugins.jetbrains.compose)
    alias(libs.plugins.android.kotlin.multiplatform.library)
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.maven.publish)
    alias(libs.plugins.dokka)
    id("androidx.baselineprofile.consumer")
}

baselineProfile {
    saveInSrc = true
    automaticGenerationDuringBuild = false
    filter { include("com.mocharealm.accompanist.lyrics.ui.**") }
    variants {
        // Android KMP profile dependencies use the extension, not an Android dependency block.
        create("androidMain") { from(project(":benchmark")) }
    }
}

kotlin {
    targets.all {
        compilations.all {
            compileTaskProvider.configure {
                compilerOptions {
                    freeCompilerArgs.add("-Xexpect-actual-classes")
                }
            }
        }
    }
    android {
        namespace = "com.mocharealm.accompanist.lyrics.ui"
        compileSdk = 37

        minSdk = 21

        optimization {
            consumerKeepRules.apply {
                publish = true
                file("consumer-rules.pro")
            }
        }

        withJava()
        withHostTestBuilder {}.configure {}
        withDeviceTestBuilder {
            sourceSetTreeName = "test"
        }

        compilations.configureEach {
            compileTaskProvider.configure {
                compilerOptions { jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21) }
            }
        }
    }
    jvm {
        compilations.configureEach {
            compileTaskProvider.configure {
                compilerOptions {
                    // Character.UnicodeBlock.ARABIC_EXTENDED_B (Java 11)
                    // and CJK_UNIFIED_IDEOGRAPHS_EXTENSION_H (Java 15)
                    // are used in String.jvm.kt. The default JVM target
                    // for the jvm() target is 1.8, which hides them and
                    // causes an unresolved reference. Match the Android
                    // target's jvmTarget here.
                    jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
                }
            }
        }
    }

    // Local cross compilation is opt-in; ordinary Windows/Linux builds keep their existing targets.
    if (System.getProperty("os.name") == "Mac OS X" ||
        providers.gradleProperty("enableIos").map(String::toBoolean).getOrElse(false)) {
        iosArm64 {
            compilations.getByName("main").cinterops.create("icu") {
                definitionFile.set(project.file("src/nativeInterop/cinterop/icu.def"))
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation(compose.runtime)
                implementation(compose.foundation)
                implementation(compose.material3)
                implementation(compose.ui)
                implementation(compose.components.uiToolingPreview)

                implementation(libs.gaze.capsule)

                implementation(libs.accompanist.lyrics.core)

                implementation(compose.components.resources)
            }
        }
        commonTest.dependencies { implementation(kotlin("test")) }
        jvmMain.dependencies { implementation(libs.icu4j) }
        jvmTest.dependencies { implementation(compose.desktop.currentOs) }
        androidMain.dependencies {
        }
        val androidDeviceTest by getting {
            dependencies { implementation("androidx.test:runner:1.5.0") }
        }
    }
}

publishing {
    repositories {
        maven {
            name = "ci"
            url = rootProject.layout.buildDirectory.dir("ci-maven").get().asFile.toURI()
        }
        maven {
            name = "local"
            url = uri("file:///E:/maven")
        }
    }
}

mavenPublishing {
    publishToMavenCentral(automaticRelease = true)

    configure(
        KotlinMultiplatform(
            javadocJar = JavadocJar.Dokka("dokkaGeneratePublicationHtml"),
            sourcesJar = true
        )
    )

    // CI archives are downloadable Maven repositories, not Maven Central uploads.
    if (!providers.gradleProperty("ciPackaging").map(String::toBoolean).getOrElse(false)) {
        signAllPublications()
    }

    coordinates("com.mocharealm.accompanist", "lyrics-ui", rootProject.version.toString())

    pom {
        name = "Accompanist Lyrics UI"
        description = "A lyrics displaying library for Compose Multiplatform"
        inceptionYear = "2025"
        url = "https://mocharealm.com/open-source"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "6xingyv"
                name = "Simon Scholz"
                url = "https://github.com/6xingyv"
            }
        }
        scm {
            url = "https://github.com/6xingyv/accompanist-lyrics-ui"
            connection = "scm:git:git://github.com/6xingyv/accompanist-lyrics-ui.git"
            developerConnection = "scm:git:ssh://git@github.com/6xingyv/accompanist-lyrics-ui.git"
        }
    }
}

composeCompiler {
    val configFile = rootProject.layout.projectDirectory.file("compose_compiler_config.conf")
    if (configFile.asFile.exists()) {
        stabilityConfigurationFiles.add(configFile)
    }
}
